let lines = readinput();

let words = vec<string>{"one", "two", "three", "four", "five", "six", "seven", "eight", "nine"};

union Word {
	Value(int),
	None,
}

fn get_word_value(str: string, part2: bool) -> Word {
	if str[0].is_digit() {
		return Word.Value(str[0].to_integer(),);
	}

	if part2 {
		for i : 0..words.len() {
			if str.starts_with(words[i]) {
				return Word.Value(i + 1,);
			}
		}
	}

	return Word.None;
}

fn get_number(str: string, part2: bool) -> int {
	let mut first = 0;
	let mut last = 0;

	let mut i = 0;
	while i < str.len() {
		if let Word.Value(v,) = get_word_value(str[i..], part2) {
			first = v;
			break;
		}
		i = i + 1;
	}

	i = str.len() - 1;
	while i >= 0 {
		if let Word.Value(v,) = get_word_value(str[i..], part2) {
			last = v;
			break;
		}
		i = i - 1;
	}

	return first * 10 + last;
}

fn get_total(part2: bool) -> int {
	let mut total = 0;
	for i : 0..lines.len() {
		total = total + get_number(lines[i], part2);
	}
	return total;
}

print(get_total(false));
print(get_total(true));

